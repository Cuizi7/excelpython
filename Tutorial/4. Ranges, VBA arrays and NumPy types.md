Ranges, lists and SAFEARRAYs
---

If you pass a VBA object to an ExcelPython functions that expect a Python object, it will get converted automatically.

That's why you can do the following

    ?PyStr(Range("A1:C1").Value2)
    [[3.0, 1.0, 2.0]]
   
The function `PyStr` applies the Python `str` function to whatever object you pass it. If that object is not already a Python object, ExcelPython will convert it for you. Equivalently, you can perform this conversion explicitly, using the `PyObj` function which converts its argument into the corresponding Python object:

    ?PyStr(PyObj(Range("A1:C1").Value2))
    [[3.0, 1.0, 2.0]]

In this specific examples, the object that gets passed is a VBA array:

    ?TypeName(Range("A1:C1").Value2)
    Variant()

Specifically, it's a 1x3 two-dimensional array:

    ?UBound(Range("A1:C1").Value2,1) & " to " & LBound(Range("A1:C1").Value2, 1)
    1 to 1
    ?UBound(Range("A1:C1").Value2,2) & " to " & LBound(Range("A1:C1").Value2, 2)
    3 to 1
  
This is an 'oddity' of Excel's VBA model - the values of 1x1 ranges are scalars:

    ?TypeName(Range("A1").Value2)
    Double
  
and all other size ranges have two-dimensional array values.

Since two-dimensional lists do not exist in Python, it gets converted to a list-of-lists. Often however, you will want to pass a range as a simple one-dimensional list. Consider for example the built-in Python function `sorted`. If you call this function on the range "A1:C1" it returns the same value as is passed in:

    ?PyStr(PyBuiltin("sorted", PyTuple(Range("A1:C1").Value2)))
    [[3.0, 1.0, 2.0]]

This is because `sorted` only sorts the top-level list, which contains only one element `[3.0, 1.0, 2.0]`.

The VBA code is equivalent to the following Python code:
```python
sorted([[3.0, 1.0, 2.0]])
```
but we want the equivalent to:
```python
sorted([3.0, 1.0, 2.0])
```

To do this, you must perform the conversion from two-dimensional VBA array to Python object explicitly, specifying the desired dimensionality of the Python object:

    ?PyStr(PyObj(Range("A1:C1").Value2, 1))
    [3.0, 1.0, 2.0]
    ?PyStr(PyBuiltin("sorted", PyTuple(PyObj(Range("A1:C1").Value2, 1))))
    [1.0, 2.0, 3.0]

NumPy arrays
--

Several people have asked me about conversion to/from NumPy types, since ExcelPython does not do this automatically.

For example, the function `scipy.stats.norm.cdf` returns a `numpy.float64` object:

    Set locals = PyDict()
    PyExec "from scipy.stats import norm", locals
    ?PyStr(PyEval("norm.cdf(0.0)", locals))
    0.5
    ?PyStr(PyEval("type(norm.cdf(0.0))", locals))
    <type 'numpy.float64'>
    ?PyVar(PyEval("norm.cdf(0.0)", locals))

The last expression causes a type mismatch, because the `PyVar` function doesn't know how to convert a `numpy.float64` object to a native VBA type and so it returns a pointer to the object itself:

    ?TypeName(PyVar(PyEval("norm.cdf(0.0)", locals)))
    IPyObj

So how do you get the value out of Python and into VBA? The key is to convert it to a type which `PyVar` _does_ know how to convert, such as `float`. Note that this can be done either within your Python code:

    ?PyVar(PyEval("float(norm.cdf(0.0))", locals))
     0.5 

or in the VBA code by calling the Python conversion function directly on the returned object:

    ?PyVar(PyBuiltin("float", PyTuple(PyEval("norm.cdf(0.0)", locals))))
     0.5

This second method, while a bit more convoluted, means your Python code does not have to be written to take into account that it could be called from Excel.

The same applies to passing in/out Numpy arrays. Suppose you have a function that takes NumPy arrays as inputs and returns them as outputs:
```python
# MatrixFunctions.py

import numpy as np

def xl_mmult(x, y):
    return x.dot(y)
```
This can be called from VBA code (without modifying the original Python code) like so:

    Public Function PyXLMult(x As Range, y As Range)
    
    On Error GoTo fail:
    
        ' Python equivalent: import numpy
        Set numpyArray = PyGet(PyModule("numpy"), "array")
    
        ' Python equivalent: x_array = numpy.array(x)
        Set x_array = PyCall(numpyArray, , PyTuple(x.Value2))
    
        ' Python equivalent: y_array = numpy.array(y)
        Set y_array = PyCall(numpyArray, , PyTuple(y.Value2))
    
        ' Python equivalent: result_array = xl_mmult(x_array, y_array)
        Set result_array = PyCall(PyModule("MatrixFunctions"), "xl_mmult", PyTuple(x_array, y_array))
    
        ' Python equivalent: result_list = result_array.tolist()
        Set result_list = PyCall(result_array, "tolist")
    
        PyXLMult = PyVar(result_list, 2)
        Exit Function
    
    fail:
        PyXLMult = Err.Description
    
    End Function
